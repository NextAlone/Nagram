apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'

repositories {
    mavenCentral()
    google()
}

configurations {
    compile.exclude module: 'support-v4'
}

configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.sharetarget:sharetarget:1.2.0-rc02"
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.google.android.gms:play-services-measurement:21.1.0'

    compileOnly 'org.checkerframework:checker-qual:3.24.0'
    compileOnly 'org.checkerframework:checker-compat-qual:2.5.5'
    implementation 'com.google.firebase:firebase-messaging:23.0.7'
    implementation 'com.google.firebase:firebase-config:21.1.1'
    implementation 'com.google.firebase:firebase-datatransport:18.1.6'
    implementation 'com.google.firebase:firebase-appindexing:20.0.0'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.gms:play-services-auth:20.2.0'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.android.gms:play-services-wearable:17.1.0'
    implementation 'com.google.android.gms:play-services-location:20.0.0'
    implementation 'com.google.android.gms:play-services-wallet:19.1.0'
    implementation 'com.googlecode.mp4parser:isoparser:1.0.6'
    implementation 'com.stripe:stripe-android:2.0.2'
    implementation 'com.google.mlkit:language-id:16.1.1'
    implementation 'com.android.billingclient:billing:5.0.0'
    implementation files('libs/libgsaverification-client.aar')

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10"
}

static def getAppID() {
    Properties properties = new Properties()
    properties.load(new FileInputStream("./API_KEYS"))

    return properties.get("APP_ID")
}

static def getAppHash() {
    Properties properties = new Properties()
    properties.load(new FileInputStream("./API_KEYS"))

    return "\"" + properties.get("APP_HASH") + "\""
}

static def getGoogleMapsAPI() {
    Properties properties = new Properties()
    properties.load(new FileInputStream("./API_KEYS"))

    return "\"" + properties.get("MAPS_V2_API") + "\""
}

android {
    compileSdkVersion 33
    buildToolsVersion '33.0.0'
    ndkVersion "21.4.7075529"
    namespace 'org.telegram.messenger'

    defaultConfig.applicationId = APP_PACKAGE
    defaultConfig.versionCode = Integer.parseInt(APP_VERSION_CODE)

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionName APP_VERSION_NAME

        // Obtain your own keys at:
        // Telegram: https://core.telegram.org/api/obtaining_api_id
        buildConfigField 'int', 'APP_ID', getAppID()
        buildConfigField 'String', 'APP_HASH', getAppHash()
        // Google Console: https://console.cloud.google.com/google/maps-apis/credentials
        resValue 'string', 'MAPS_V2_API', getGoogleMapsAPI()

        externalNativeBuild {
            cmake {
                version '3.10.2'
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21'
            }
        }
    }

    sourceSets.main.jniLibs.srcDirs = ['./jni/']

    /*sourceSets.main {
        jniLibs.srcDir 'libs'
        jni.srcDirs = []
    }*/

    externalNativeBuild {
        cmake {
            path 'jni/CMakeLists.txt'
        }
    }

    lint {
        checkReleaseBuilds false
        disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11

        coreLibraryDesugaringEnabled true
    }

    signingConfigs {
        release {
            storeFile file("config/extera.jks")
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    buildTypes {
        debug {
            debuggable false
            jniDebuggable true
            applicationIdSuffix ".beta"
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk.debugSymbolLevel = 'FULL'
            buildConfigField 'boolean', 'DEBUG_VERSION', 'true'
        }

        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk.debugSymbolLevel = 'FULL'
            crunchPngs false
        }
    }

    sourceSets.debug {
        manifest.srcFile 'config/debug/AndroidManifest.xml'
    }

    sourceSets.release {
        manifest.srcFile 'config/release/AndroidManifest.xml'
    }

    flavorDimensions "minApi"

    productFlavors {
        armv7 {
            ndk {
                abiFilters "armeabi-v7a"
            }
            ext {
                abiVersionCode = 1
            }
        }
        x86 {
            ndk {
                abiFilters "x86"
            }
            ext {
                abiVersionCode = 2
            }
        }
        arm64 {
            ndk {
                abiFilters "arm64-v8a"
            }
            ext {
                abiVersionCode = 3
            }
        }
        x64 {
            ndk {
                abiFilters "x86_64"
            }
            ext {
                abiVersionCode = 4
            }
        }
        afat {
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
            sourceSets.release {
                manifest.srcFile 'config/release/AndroidManifest.xml'
            }
            ext {
                abiVersionCode = 5
            }
        }
        beta {
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a"
            }
            sourceSets.debug {
                manifest.srcFile 'config/debug/AndroidManifest.xml'
            }
            ext {
                abiVersionCode = 6
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def buildType = variant.productFlavors[0].name
            if (variant.productFlavors[0].name != "beta") {
                buildType = "release"
            }
            def date = new Date()
            def formattedDate = date.format('ddMMyyyy')

            outputFileName = "exteragram-${buildType}-${formattedDate}.apk"
            output.versionCodeOverride = defaultConfig.versionCode * 10 + variant.productFlavors.get(0).abiVersionCode
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name != "release" && !names.contains("beta")) {
            setIgnore(true)
        }
        if (variant.buildType.name != "debug" && names.contains("beta")) {
            setIgnore(true)
        }
    }

    dependenciesInfo.includeInApk false
}

apply plugin: 'com.google.gms.google-services'